buildscript {
    ext {
        jvmTarget = JavaVersion.VERSION_11
        kotlinVersion = '1.5.30'
        ktlintVersion = "10.2.1"
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.14.0"
    }
}


plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id "org.jlleitschuh.gradle.ktlint" version "${ktlintVersion}"
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'gradle.plugin.com.betomorrow.gradle'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly gradleApi()

    // Third Party
    implementation 'com.google.api-client:google-api-client:1.27.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.27.0'
    implementation 'com.google.apis:google-api-services-drive:v3-rev136-1.25.0'
    implementation 'org.apache.commons:commons-compress:1.18'
    implementation 'org.apache.poi:poi-ooxml:4.0.1'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.3.1")
    testImplementation('org.jetbrains.kotlin:kotlin-test-junit5:1.5.30')

    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation gradleTestKit()

    testRuntimeOnly "cglib:cglib-nodep:3.2.4"          // allows mocking of classes (in addition to interfaces)
    testRuntimeOnly "org.objenesis:objenesis:2.4"    // allows mocking of classes without default constructor (together with CGLIB)
}

sourceCompatibility = project.ext.jvmTarget
targetCompatibility = project.ext.jvmTarget

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = project.ext.jvmTarget.toString()
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = project.ext.jvmTarget.toString()
    }
}

test {
    useJUnitPlatform {}
}

/**
 * Integration test
 */
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
            sourceCompatibility = project.ext.jvmTarget
            targetCompatibility = project.ext.jvmTarget
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    outputs.upToDateWhen { false }
}

compileIntegrationTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = project.ext.jvmTarget.toString()
    }
}

check.dependsOn integrationTest

gradlePlugin {
    testSourceSets sourceSets.integrationTest
}

/**
 * Publishing
 */

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
}

wrapper {
    gradleVersion = "7.2"
    distributionType = Wrapper.DistributionType.ALL
}

pluginBundle {

    website = 'https://github.com/oliviergauthier/gradle-wording-plugin'
    vcsUrl = 'https://github.com/oliviergauthier/gradle-wording-plugin'
    tags = ['android', 'spring', 'wording', 'plugin', 'tool']

    plugins {

        libraryPlugin {
            id = 'com.betomorrow.gradle.wording'
            displayName = 'Gradle Wording Plugin'
            description = 'Plugin to import wording from Google Sheet then integrate it in standard translations files.'
        }
    }
}
